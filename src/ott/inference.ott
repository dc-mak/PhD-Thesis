% These rules infer indices, which the implementation currently does not (but did at one point)

grammar

  res_diff {{ tex \outpol{ \cnnt{res\_diff} } }} :: 'Res_diff_' ::= {{ com resource difference }}
    | None                                                                                        :: :: None
      {{ com not possible to take a difference }} {{ tex \outpol{[[None]]} }}
    | out_res_term and out_oarg                                                                   :: :: Clean
      {{ com request is satisfied exactly by $[[out_res_term]]$ and the output argument is $[[out_oarg]]$ }}
    | out_oarg and out_pred_ipred                                                                    :: :: More
      {{ com request is satisfied partially with output argument $[[out_oarg]]$ with remaining $[[out_pred_ipred]]$ }}
    | bind out_res_bind for out_x1 & out_oarg REM out_x2 : out_rem     :: :: Rem
      {{ com use $[[out_res_bind]]$ to satisfy request exactly (using $[[out_x1]]$ and $[[out_oarg]]$) with remainder $[[out_x2]] [[:]] [[out_rem]]$ }}


  res_bind :: 'res_bind_' ::= {{ com resource bindings }}
    | cdot                      :: :: nil    {{ com empty resource binding                                       }}
    | res_pat : res = res_term  :: :: cons   {{ com match $[[res_term]] [[:]] [[res]]$ against $[[res_pat]]$ }}
    | </ res_bindi // , // i /> :: :: concat {{ com concatenate resource bindings                                }}


  % OUT_HACK|res_bind,\cnnt{res\_bind}
  out_res_bind {{ tex \outpol{ \cnnt{res\_bind} } }}   :: 'Out_hack_' ::=  {{ com Ott-hack, ignore }}
    | res_bind :: :: res_bind {{ tex \outpol{ [[res_bind]] } }}

  opt_term :: 'Opt_term_' ::= {{ com optional SMT term }}
    | None :: :: None
    | term :: :: Some

  % OUT_HACK|opt_term,\cnnt{opt\_term}
  out_opt_term {{ tex \outpol{ \cnnt{opt\_term} } }}   :: 'Out_hack_' ::=  {{ com Ott-hack, ignore }}
    | opt_term :: :: opt_term {{ tex \outpol{ [[opt_term]] } }}

  cmp :: 'Cmp_' ::= {{ com result of binary comparison }}
    | Lt :: :: Lt {{ com less-than    }}
    | Eq :: :: Eq {{ com equals       }}
    | Gt :: :: Gt {{ com greater-than }}

  opt_cmp :: 'Opt_cmp_' ::= {{ com optional result of binary comparison }}
    | None :: :: None
    | cmp  :: :: Some

  % OUT_HACK|opt_cmp,\cnnt{opt\_cmp}
  out_opt_cmp {{ tex \outpol{ \cnnt{opt\_cmp} } }}   :: 'Out_hack_' ::=  {{ com Ott-hack, ignore }}
    | opt_cmp :: :: opt_cmp {{ tex \outpol{ [[opt_cmp]] } }}

  opt_cmp_term :: 'Opt_cmp_term_' ::= {{ com optional result of binary comparison and SMT term }}
    | None       :: :: None
    | cmp , term :: :: Some

  % OUT_HACK|opt_cmp_term,\cnnt{opt\_cmp\_term}
  out_opt_cmp_term {{ tex \outpol{ \cnnt{opt\_cmp\_term} } }}   :: 'Out_hack_' ::=  {{ com Ott-hack, ignore }}
    | opt_cmp_term :: :: opt_cmp_term {{ tex \outpol{ [[opt_cmp_term]] } }}

defns
  inf_res :: '' ::=

  defn N |- pred_term in? ipred_term ~> out_opt_term :: :: PinI :: PinI_
  {{ com given constraints $[[N]]$, $[[pred_term]]$ is potentially a part of $[[ipred_term]]$ at index $[[out_opt_term]]$ }} by

    a1 != a2
    ----------------------------------------------------------------------- :: Name_Neq
    N |- a1 ( _iargs ) in? each (integer _x ; _t) { a2 ( _iargs ) } ~> None


    term def= ( ptrt2 - ptrt1 ) / sizeof ct
    term1 def= term / x ( iguard )
    term2 def= /\\ ( </ iarg1i = [ term / x ] ( iarg2i ) // i /> )
    smt ( N => neg ( term1 /\ term2 ) )
    -------------------------------------------------------------------------- :: IG_Or_IArg_Neq
    N |- a ( ptrt2 , </ iarg1i // i /> ) in? each (integer x; iguard) { a ( array_shift < ct > ( ptrt1 , x ) , </ iarg2i // i /> ) }  ~> None


    term def= ( ptrt2 - ptrt1 ) / sizeof ct
    term1 def= term / x ( iguard )
    term2 def= /\\ ( </  iarg1i = [ term / x ] ( iarg2i ) // i /> )
    smt ( N => term1 /\ term2 )
    -------------------------------------------------------------------------- :: Comp
    N |- a ( ptrt2 , </ iarg1i // i /> ) in? each (integer x; iguard) { a ( array_shift < ct > ( ptrt1 , x ) , </ iarg2i // i /> ) } ~> term


  defn N |- r : norm_res -? pred_ipred ~> res_diff :: :: Res_Diff :: Res_Diff_
  {{ com the difference between $[[r]] [[:]] [[norm_res]]$ and requested $[[pred_ipred]]$ is $[[res_diff]]$ }} by

    ---------------------------------------------------------- :: If_None
    N |- _x : if term then res1 else res2 -? pred_ipred ~> None


    N |- pred_term = pred_term' ~> false
    ----------------------------------------------- :: PP_None
    N |- _x : pred_term' ( _x ) -? pred_term  ~> None


    N |- pred_term = pred_term' ~> true
    -------------------------------------------------------- :: PP_Exact
    N |- r : pred_term' ( oarg ) -? pred_term ~> r and oarg


    N |- pred_term in? ipred_term ~> None
    ------------------------------------------------ :: PI_None
    N |- _x : ipred_term ( _t ) -? pred_term ~> None


    N |- pred_term in? ipred_term ~> term
    ipred_term def= each (integer x; iguard) { pred_term }
    rem def= each (integer x; iguard /\ (x != term)) { pred_term } ( oarg )
    oarg' def= oarg [ term ]
    res def= pred_term ( oarg' )
    -------------------------------------------------------------------------- :: PI_Rem
    N |- r : ipred_term ( oarg ) -? pred_term ~> bind < r1 , r2 > : rem * res = break ( r , term ) for r2 & oarg' REM r1 : rem


    N |- pred_term in? ipred_term ~> None
    ------------------------------------------------ :: IP_None
    N |- _x : pred_term ( _t ) -? ipred_term ~> None


    N |- pred_term in? ipred_term ~> term
    ipred_term def= each (integer x; iguard) { pred_term }
    smt ( N => neg < each (integer x; _t) { neg ( iguard /\ ( x != term ) ) } > )
    ipred_term' def= each (integer x; iguard /\ ( x != term ) ) { pred_term }
    ----------------------------------------------------------------- :: IP_More
    N |- r : pred_term ( oarg ) -? ipred_term ~> oarg and ipred_term'


    pred_term def= r ( array_shift < ct > ( ptrt1 , x ) , iargs )
    N |- pred_term in? ipred_term ~> term
    ipred_term def= each (integer x; iguard) { pred_term }
    smt ( N => each (integer x; _t) { neg ( iguard /\ ( x != term ) ) } )
    C ; L |- oarg => base_type
    ------------------------------------------------------------------------------------------------ :: IP_Last
    N |- r : pred_term ( oarg ) -? ipred_term ~> inj ( r , ptrt1 , ct , x .: iargs ) and const oarg


    N |- ipred_term [=? ipred_term' ~> None
    -------------------------------------------------- :: II_None
    N |- _x : ipred_term' ( _t ) -? ipred_term ~> None


    N |- ipred_term' [=? ipred_term ~> Eq
    ------------------------------------------------------------------- :: II_Eq
    N |- r : ipred_term ( oarg ) -? ipred_term'  ~> r and oarg


    N |- ipred_term' [=? ipred_term ~> Lt
    % ipred_term' < ipred_term
    ipred_term def= each (integer x; iguard) { pred_term }
    ipred_term' def= each (integer x; iguard') { pred_term }
    rem def= each (integer x; iguard  /\ neg iguard') { pred_term } ( oarg )
    ---------------------------------------------------------------------------------------- :: II_Lt
    N |- r : ipred_term ( oarg ) -? ipred_term' ~> bind < r1 , r2 > : res = chop ( r , iguard' ) for r1 & oarg [ k ] REM r2 : rem


    % This would require being able to merge arrays, which the implementation does not support
    % N |- ipred_term' [=? ipred_term ~> Gt
    % % ipred_term' > ipred_term
    % ipred_term  def= { x ; iguard  .: a ( ptrt + step , iargs ) }
    % ipred_term' def= { x ; iguard' .: a ( ptrt + step , iargs ) }
    % ------------------------------------------------------------------ :: II_Gt
    % N |- r : ipred_term & oarg -? ipred_term'  ~> oarg and { x ; iguard' /\ neg iguard .: a ( ptrt + step , iargs ) }


  defn N |- r1 : norm_res +? res_term2 : pred_ipred & oarg2 ~> out_res_term and out_oarg3 :: :: Res_Comb :: Res_Comb_
  {{ com given constraints $[[N]]$, combining $[[r1]] [[:]] [[norm_res]]$, $[[res_term2]] [[:]] [[pred_ipred]] \& [[oarg2]]$, results in $[[out_res_term]]$ $[[out_oarg3]]$ }} by

    N |- pred_term in? ipred_term ~> term
    C ; L |- oarg1 => base_type
    C ; L |- oarg2 => array base_type
    ---------------------------------------------------------- :: PI
    N |- r : pred_term ( oarg1 ) +? res_term : ipred_term & oarg2 ~> glue( < res_term , r > ) and oarg2 [ term ] := oarg1


  defn N ; nR |- pred_ipred ~> bind out_res_bind for out_res_term and out_oarg -| out_nRp :: :: Req :: Req_
  {{ com constraint $[[N]]$ and resource $[[nR]]$ contexts fulfil request $[[pred_ipred]]$ (via $[[out_res_bind]]$) for answer $[[out_res_term]]$ and $[[out_oarg]]$, with $[[out_nRp]]$ leftover }} by


    N |- r : norm_res -? pred_ipred ~> None
    N ; nR |- pred_ipred ~> bind res_bind for res_term and oarg -| nR
    ------------------------------------------------------------------------------------------------ :: Rej
    N ; nR , r : norm_res |- pred_ipred ~> bind res_bind for res_term and oarg -| nR , r : norm_res


    N |- r : norm_res -? pred_ipred ~> res_term and oarg
    ----------------------------------------------------------------------------- :: Acc_Clean
    N ; nR , r : norm_res |- pred_ipred ~> bind cdot for res_term and oarg -| nR


    N |- r : norm_res -? pred_ipred ~> bind res_pat1 : res1 = res_term1 for r1 & oarg REM r2 : rem
    -------------------------------------------------------------------------------------------------------------------- :: Acc_Rem
    N ; nR , r : norm_res |- pred_ipred ~> bind res_pat1 : res1 = res_term1 , cdot for r1 and oarg -| nR , r2 : rem


    N |- r : norm_res -? pred_ipred1 ~> oarg1 and pred_ipred2
    N ; nR |- pred_ipred2 ~> bind res_bind2 for res_term2 and oarg2 -| nR2
    N |- r : norm_res +? res_term2 : pred_ipred2 & oarg2 ~> res_term3 and oarg3
    -------------------------------------------------------------------------------------- :: Acc_More
    N ; nR , r : norm_res |- pred_ipred1 ~> bind res_bind for res_term3 and oarg3 -| nR2


  defn N ; nR |- if term then res1 else res2 ~> out_x -| out_nRp :: :: If :: If_
  {{ com constraint $[[N]]$ and resource $[[nR]]$ contexts fulfil request for $[[if]]$ $[[term]]$ $[[then]]$ $[[res1]]$ $[[else]]$ $[[res2]]$ with \textbf{synthesising} $[[out_x]]$ and $[[out_nRp]]$ leftover }} by


    N |- norm_res = if term then res1 else res2
    -------------------------------------------------------------- :: Acc
    N ; nR , x : norm_res |- if term then res1 else res2 ~> x -| nR


    N ; nR |- if term then res1 else res2 ~> x -| nR
    ----------------------------------------------------------------------------------- :: Rej
    N ; nR , x : norm_res |- if term then res1 else res2 ~> x -| nR , x : norm_res


  defn y ; N ; nR |- res ~> bind out_res_bind for out_res_term and out_oarg -| out_nRp :: :: Oarg :: Oarg_
  {{ com constraint $[[N]]$ and resource $[[nR]]$ contexts fulfil request for output argument $[[y]]$ of resource $[[res]]$ (via $[[out_res_bind]]$) with \textbf{checking} $[[out_res_term]]$ and $[[out_oarg]]$, leaving resources $[[out_nRp]]$}} by


    % ------------------------------------------------- :: Empty
    % N ; nR |- emp ~> bind cdot for emp and x -| nR


    ---------------------------------------------------- :: Return
    y ; N ; nR |- y = term ~> bind cdot for emp and term -| nR


    N ; nR |- pred_term ~> bind res_bind for res_term and oarg -| nR'
    ------------------------------------------------------------------------ :: Pred
    y ; N ; nR |- pred_term ( y ) ~> bind res_bind for res_term and oarg  -| nR'


    N ; nR |- ipred_term ~> bind res_bind for res_term and oarg -| nR'
    ------------------------------------------------------------------------- :: IPred
    y ; N ; nR |- ipred_term ( y ) ~> bind res_bind for res_term and oarg  -| nR'
    

    smt ( N => term )
    y ; N ; nR |- res ~> bind res_bind for res_term and oarg -| nR'
    ----------------------------------------------------------------------------- :: Assert
    y ; N ; nR |- term * res ~> bind res_bind for < TERM , res_term > and oarg -| nR'


    smt ( N => term )
    y ; N ; nR |- res1 ~> bind res_bind for res_term and oarg -| nR'
    ----------------------------------------------------------------------------------- :: If_True
    y ; N ; nR |- if term then res1 else res2 ~> bind res_bind for res_term and oarg -| nR'


    smt ( N => neg term )
    y ; N ; nR |- res2 ~> bind res_bind for res_term and oarg -| nR'
    ----------------------------------------------------------------------------------- :: If_False
    y ; N ; nR |- if term then res1 else res2 ~> bind res_bind for res_term and oarg -| nR'


    --------------------------------------------------------------------- :: If_Undet
    y ; N ; nR |- if term then res1 else res2 ~> bind cdot for y and y -| nR'


    y1 ; N ; nR |- res1 ~> bind res_bind1 for res_term1 and oarg1 -| nR1
    y ; N ; nR1 |- oarg1 / y1 ( res2 ) ~> bind res_bind2 for res_term2 and oarg2 -| nR2
    res_term def= pack ( oarg2 , < res_term1 , res_term2 > )
    ---------------------------------------------------------------------------------------------------------- :: Take
    y ; N ; nR |- exists y1 : base_type .: res1 * res2 ~> bind res_bind1 , res_bind2 for res_term and oarg2  -| nR2


defns
  elab_action :: 'Elab_' ::=

  % DEFER out_res
  defn CLNnR |- action ~> bind out_res_bind for out_res_term & out_norm_ret -| out_nRp :: :: Action :: Action_
  {{ com given $[[CLNnR]]$ and $[[action]]$, infer (via $[[out_res_bind]]$) $[[out_res_term]]$ and $[[out_norm_ret]]$, with $[[out_nRp]]$ leftover }} by


    C ; L ; N ; nR |- emp | create < ct > ( pval ) => norm_ret
    norm_ret def= sigma yp : loc .: term /\ exists x : to_base ( ct ) .: ( yp |- Uninit - ct -> x ) *o I
    term def= representable ( ct * , yp ) /\ aligned ( inj pval , yp )
    ----------------------------------------------------------------------- :: Create
    C ; L ; N ; nR |- create < ct > ( pval ) ~> bind cdot for emp & norm_ret -| nR


    % DEFER (not implemented)
    % C ; L ; N ; cdot |-> create_readonly ( vp , ct , v ) Symbol_prefix => ??


    % DEFER (not implemented) - memory order
    C |- pval0 => loc
    N ; nR |- Owned < Init , ct > ( inj pval0 ) ~> bind res_bind for res_term and oarg -| nR'
    norm_ret def= sigma y : to_base ( ct ) .: y = oarg /\ pt *o I
    -------------------------------------------------------------------------- :: Load
    C ; L ; N ; nR |- load < ct >  ( pval0 ) ~> bind res_bind for res_term & norm_ret -| nR'


    C |- pval0 => loc
    C |- pval1 => to_base ( ct )
    % DEFER (not implemented) - locking boolean
    smt ( N => representable ( ct , inj pval1 ) )
    N ; nR |- Owned < _init , ct > ( inj pval0 ) ~> bind res_bind for res_term and _t -| nR'
    norm_ret def= sigma _x : unit .: ( inj pval0 |- Init - ct -> inj pval1 ) *o I
    ------------------------------------------------------------------------------------------- :: Store
    C ; L ; N ; nR |- store < ct > ( pval0 , pval1 ) ~> bind res_bind for res_term & norm_ret -| nR'


    C |- pval0 => loc
    N ; nR |- Owned < _init , ct > ( inj pval ) ~> bind res_bind for res_term and _t -| nR'
    ---------------------------------------------------------------------------------------- :: Kill_Static
    C ; L ; N ; nR |- kill < ct > ( pval ) ~> bind res_bind for res_term & sigma _x : unit .: I -| nR'


    % DEFER (not implemented)
    % C ; L ; N ; ?? |- _rest | kill < dyn > ( v ) => ??

defns
  elab_memop :: 'Elab_' ::=

  defn CLNnR |- pval1 , pval2 ~> out_res_term : out_norm_res -| out_nRp :: :: LiveBounds :: LiveBounds_
  {{ com given $[[CLNnR]]$, infer, for pointers $[[pval1]], [[pval2]]$, proof of bounds and liveness $[[out_res_term]] {:} [[out_norm_res]]$, with $[[out_nRp]]$ leftover }} by


  smt ( N => term = inj pval1 . alloc_id = inj pval2 . alloc_id )
  smt ( N => oarg . base =< inj pval1 . addr =< oarg . base + oarg . len )
  smt ( N => oarg . base =< inj pval2 . addr =< oarg . base + oarg . len )
  ---------------------------------------------------------------------------------------- :: Alloc
  C ; L ; N ; r : Alloc ( term ) ( oarg ) , nR |- pval1 , pval2 ~> r : Alloc ( term ) ( oarg ) -| nR 


  smt ( N => ptrt . alloc_id = inj pval1 . alloc_id = inj pval2 . alloc_id )
  term def= allocv [ inj pval1 . alloc_id ]
  smt ( N => term . base =< inj pval1 . addr =< term . base + term . len )
  smt ( N => term . base =< inj pval2 . addr =< term . base + term . len )
  ---------------------------------------------------------------------------------------- :: Owned
  C ; L ; N ; r : Owned < _init , ct > ( ptrt ) ( oarg ) , nR |- pval1 , pval2 ~> r : Owned < _init , ct > ( ptrt ) ( oarg ) -| nR 


  C ; L ; N ; nR |- pval1 , pval2  ~> res_term : norm_res -| nR'
  --------------------------------------------------------- :: Other
  C ; L ; N ; _x : norm_res , nR |- pval1 , pval2 ~> res_term : norm_res -| nR'


  defn CLNnR |- memop ~> bind out_res_bind for out_res_term & out_ret -| out_nRp :: :: Memop :: Memop_
  {{ com given $[[CLNnR]]$ and $[[memop]]$, infer (via $[[out_res_bind]]$) $[[out_res_term]]$ and $[[out_ret]]$, with $[[out_nRp]]$ leftover }} by


  C |- pval1 => loc
  C |- pval2 => loc
  norm_ret def= sigma y : bool .: term1 /\ term3 *o I
  term1 def= inj pval1 = inj pval2 -> y
  term2 def= inj pval1 . alloc_id != inj pval2 . alloc_id /\ inj pval1 . addr = inj pval2 . addr
  term3 def= neg term1 /\ neg term2 -> neg y
  --------------------------------------------------------------- :: PtrEq
  C ; L ; N ; nR |- pval1 = pval2 ~> bind cdot for emp & norm_ret -| nR


  C |- pval1 => loc
  C |- pval2 => loc
  C ; L ; N ; nR |- pval1 , pval2 ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : bool .: y = ( inj pval1 . addr ' rel_binop ' inj pval2 . addr ) /\ res *o I
  ----------------------------------------------------------------------------- :: Rel_Binop
  C ; L ; N ; nR |- pval1 rel_binop pval2 ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval1 => loc
  C |- pval2 => loc
  C ; L ; N ; nR |- pval1 , pval2 ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : integer .: y = ( inj pval1 . addr - inj pval2 .addr ) / sizeof ct /\ norm_res *o I
  ---------------------------------------------------------------------------------------------------------- :: PtrDiff_Arr
  C ; L ; N ; nR |- pval1 - array n ct - pval2 ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval1 => loc
  C |- pval2 => loc
  C ; L ; N ; nR |- pval1 , pval2 ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : integer .: y = ( inj pval1 . addr - inj pval2 . addr ) / sizeof ct /\ norm_res *o I
  ----------------------------------------------------------------------------------------------------- :: PtrDiff
  C ; L ; N ; nR |- pval1 - ct - pval2 ~> bind cdot for res_term & norm_ret -| nR'


  % Doesn't require a bounds check in VIP
  C |- pval => loc
  C ; L ; N ; nR |- pval , pval ~> res_term : norm_res -| nR'
  smt ( N => representable ( ct , inj pval . addr ) )
  norm_ret def= sigma y : integer .: y = inj pval . addr /\ norm_res *o I
  ----------------------------------------------------------------------- :: IntFromRt
  C ; L ; N ; nR |- intFromRt < ct > ( pval ) ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval => integer
  C ; L ; N ; nR |- pval , pval ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : loc .: exists x : alloc_id .: y = ( @ x , inj pval ) /\ norm_res *o I
  ----------------------------------- :: RtFromInt
  C ; L ; N ; nR |- rtFromInt ( pval ) ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval => loc
  norm_ret def= sigma y : loc .: exists x : alloc_id .: y = term /\ term' /\ I
  term def= if inj pval = NULL then ( @ x , 0 ) else inj pval
  term' def= inj pval = NULL \/ x = inj pval . alloc_id
  ------------------------------------------------ :: RtFromPtr
  C ; L ; N ; nR |- rtFromPtr ( pval ) ~> bind cdot for emp & norm_ret -| nR


  C |- pval => integer
  smt ( N => inj pval . addr != 0 )
  C ; L ; N ; nR |- pval , pval ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : loc .: y = inj pval /\ norm_res *o I
  ----------------------------------- :: LiveFromRt
  C ; L ; N ; nR |- ptrFromRt < ct > ( pval ) ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval => integer
  norm_ret def= sigma y : integer .: exists x : alloc_id .: y = term /\ I
  term def= if inj pval . addr = 0 then NULL else ( @ x , inj pval . addr )
  ----------------------------------- :: UnsureFromRt
  C ; L ; N ; nR |- ptrFromRt < ct > ( pval ) ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval1 => integer
  C |- pval2 => loc
  inj ptr def= ( @ inj pval2 . alloc_id , inj pval1 )
  C ; L ; N ; nR |- ptr , ptr ~> res_term : norm_res -| nR'
  ret def= sigma y : loc .: y = inj ptr /\ norm_res *o I
  ----------------------------------- :: CopyAllocId
  C ; L ; N ; nR |- copyAllocId ( pval1 , pval2 ) ~> bind cdot for res_term & norm_ret -| nR'


  % See comment in explicit.ott typing rule!
  C |- pval => loc
  C ; L ; N ; nR |- pval , pval ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : bool .: y = aligned ( sizeof ct , inj pval ) /\ norm_res *o I
  ---------------- :: PtrValidForDeref
  C ; L ; N ; nR |- ptrValidForDeref < ct > ( pval ) ~> bind cdot for res_term & norm_ret -| nR


  C |- pval => loc
  norm_ret def= sigma y : bool .: I
  ---------------- :: PtrUnsureForDeref
  C ; L ; N ; nR |- ptrValidForDeref < ct > ( pval ) ~> bind cdot for emp & norm_ret -| nR


  C |- pval => loc
  norm_ret def= sigma y : bool .: y = aligned ( sizeof ct , inj pval ) /\ I
  ----------------- :: PtrWellAligned
  C ; L ; N ; nR |- ptrWellAligned < ct > ( pval ) ~> bind cdot for emp & norm_ret -| nR


  C |- pval1 => loc
  C |- pval2 => integer
  C ; L ; N ; nR |- pval , pval ~> res_term : norm_res -| nR'
  norm_ret def= sigma y : loc .: y = inj ptr /\ norm_res *o I
  -------------------------------------------------------------------------- :: PtrArrayShift
  C ; L ; N ; nR |- ptrArrayShift < ct > ( pval1 , pval2 ) ~> bind cdot for res_term & norm_ret -| nR'


  C |- pval1 => loc
  C |- pval2 => loc
  C |- pval3 => integer
  ipred_term1 def= each (integer x; 0 =< x =< inj pval3 - 1) { Owned < Uninit , byte > ( array_shift < byte > ( inj pval1 , x ) ) }
  ipred_term2 def= each (integer x; 0 =< x =< inj pval3 - 1) { Owned <   Init , byte > ( array_shift < byte > ( inj pval2 , x ) ) }
  N ; nR  |- ipred_term1 ~> bind res_bind1 for res_term1 and _t   -| nR1
  N ; nR1 |- ipred_term2 ~> bind res_bind2 for res_term2 and oarg -| nR2
  ret def= sigma y : loc .: y = inj pval1 /\ ipred_term1 ( oarg ) * ipred_term2 ( oarg ) *o I
  ----------------------------------------------------------------------------- :: Memcpy
  C ; L ; N ; nR |- memcpy ( pval1 , pval2 , pval3 ) ~> bind res_bind1, res_bind2 for < res_term1 , res_term2 > & ret -| nR2


  C |- pval1 => loc
  C |- pval2 => loc
  C |- pval3 => integer
  ipred_term1 def= each (integer x; 0 =< x =< inj pval3 - 1) { Owned < Init , byte > ( array_shift < byte > ( inj pval1 , x ) ) }
  ipred_term2 def= each (integer x; 0 =< x =< inj pval3 - 1) { Owned < Init , byte > ( array_shift < byte > ( inj pval2 , x ) ) }
  N ; nR  |- ipred_term1 ~> bind res_bind1 for res_term1 and oarg1 -| nR1
  N ; nR1 |- ipred_term2 ~> bind res_bind2 for res_term2 and oarg2 -| nR2
  smt ( N => each (integer x; 0 =< x =< inj pval3 - 1) { oarg1 [ x ] != inj Unspecified () } )
  smt ( N => each (integer x; 0 =< x =< inj pval3 - 1) { oarg2 [ x ] != inj Unspecified () } )
  smt ( N => 0 =< inj pval3 )
  ret def= sigma y : integer .: y = memcmp ( inj pval1 , inj pval2 , inj pval3 ) /\ ipred_term1 ( oarg1 ) * ipred_term2 ( oarg2 ) *o I
  ----------------------------------------------------------------------------- :: Memcmp
  C ; L ; N ; nR |- memcmp ( pval1 , pval2 , pval3 ) ~> bind res_bind1 , res_bind2 for < res_term1 , res_term2 > & ret -| nR2

defns
  elab_spine :: 'Elab_' ::=

  % DEFER out_spine'
  defn CLNnR |- spine '::' norm_fun ~> bind out_res_bind for spine' and out_norm_ret -| out_nRp :: :: Spine :: Spine_
  {{ com given $[[CLNnR]]$, $[[spine]]$ and function type $[[norm_fun]]$ elaborate (via $[[out_res_bind]]$) to $[[spine']]$ and result type $[[out_norm_ret]]$ , with $[[out_nRp]]$ leftover }} by

    -------------------------------------------------------------- :: Empty
    C ; L ; N ; nR |- :: norm_ret ~> bind cdot for and norm_ret -| nR


    C |- pval => base_type
    C ; L ; N ; nR |- spine :: pval / x  ( norm_fun ) ~> bind res_bind for spine' and norm_ret -| nR'
    ---------------------------------------------------------------------------------------------------------------- :: Comp
    C ; L ; N ; nR |- pval , spine :: pi x : base_type .: norm_fun ~> bind res_bind for pval , spine' and norm_ret -| nR'


    y ; N ; nR |- norm_res ~> bind res_bind for res_term and oarg -| nR'
    C ; L ; N ; nR' |- spine :: oarg / y ( norm_fun ) ~> bind res_bind' for spine' and norm_ret -| nR''
    ------------------------------------------------------------------------------------------- :: Take
    C ; L ; N ; nR |- spine :: forall y : base_type .: norm_res --o norm_fun ~> bind res_bind , res_bind' for oarg , res_term , spine' and norm_ret -| nR''


    smt ( N => term )
    C ; L ; N ; nR |- spine :: norm_fun ~> bind res_bind for spine' and norm_ret -| nR'
    ------------------------------------------------------------------------------------------- :: Phi
    C ; L ; N ; nR |- spine ::  term -) norm_fun ~> bind res_bind for spine' and norm_ret -| nR'


defns
  elab_expr :: 'Elab_' ::=

  defn CLNnR |- expr ~> bind out_res_bind for expr' : out_ret -| out_nRp :: :: Expr :: Expr_
  {{ com given $[[CLNnR]]$, $[[expr]]$ elaborates (via $[[out_res_bind]]$) to $[[expr']] [[:]] [[out_ret]]$, with $[[out_nRp]]$ leftover }} by


    C ; L ; N |- pexpr => pure_ret
    ---------------------------------------------------------------------------- :: Pure
    C ; L ; N ; nR |- pure ( pexpr ) ~> bind cdot for pure ( pexpr ) : pure_ret -| nR


    C ; L ; N ; nR |- action ~> bind res_bind for res_term & norm_ret -| nR'
    --------------------------------------------------------------------------- :: Action
    C ; L ; N ; nR |- _rest | action ~> bind res_bind for res_term | action : norm_ret -| nR'


    C ; L ; N ; nR |- memop ~> bind res_bind for res_term & ret -| nR'
    ------------------------------------------------------------------------- :: Memop
    C ; L ; N ; nR |- _rest | memop ~> bind res_bind for res_term | memop : ret -| nR'


    % </ C ; L ; N ; nRi |- stmti <= reti // i />
    % -------------------------------------------------------------------------------------------- :: Unseq
    % C ; L ; N ; </ nRi // i /> |- unseq ( </ stmti : reti // i /> ) => concat </ reti // i />


    P ( id ) = </ xi // i /> |-> stmt : norm_fun
    C ; L ; N ; nR |- spine :: norm_fun ~> bind res_bind for </ spine_elemi // i /> and norm_ret -| nR'
    ---------------------------------------------------------------------------------------------------------------- :: CCall
    C ; L ; N ; nR |- ccall ( ct , id , spine ) ~> bind res_bind for ccall ( ct , id , </ spine_elemi // i /> ) : norm_ret -| nR'


    P ( name ) = </ xi // i /> |-> stmt : norm_fun
    C ; L ; N ; nR |- spine :: norm_fun ~> bind res_bind for </ spine_elemi // i /> and norm_ret -| nR'
    ------------------------------------------------------------------------------------------------------------------ :: Proc
    C ; L ; N ; nR |- pcall ( name , spine ) ~> bind res_bind for pcall ( name , </ spine_elemi // i /> ) : norm_ret -| nR'


    P ( a ) = </ xi : base_typei // i  /> , y : _bt |-> res
    % indexed </ C |- pvali => base_typei // i />
    y ; N ; nR |- [ </ pvali / xi // i /> ] ( res ) ~> bind res_bind for res_term and oarg -| nR'
    pred def= a ( </ inj pvali // i /> ) ( oarg )
    res_term' def= fold res_term : pred
    ---------------------------------------------------------------------------------------------------------- :: Fold
    C ; L ; N ; nR |- fold a ( </ pvali // i /> ) ~> bind res_bind for res_term' : pred *o I -| nR''


    P ( a ) = </ xi : base_typei // i  /> , _x : _bt |-> res
    % indexed </ C |- pvali => base_typei // i />
    N ; nR |- a ( </ inj pvali // i /> ) ~> bind res_bind for res_term and oarg -| nR'
    res' def= [ oarg / y , </ iargi / xi // i /> ] ( res )
    ------------------------------------------------------------------------------------------------ :: Unfold
    C ; L ; N ; nR |- unfold a ( </ pvali // i /> ) ~> bind res_bind , fold ( r ) : a ( </ inj pvali // i /> ) ( oarg ) = res_term for r : res' *o I -| nR'


defns
  elab :: 'Elab_' ::=

  defn N |- res ~> out_res_pat :: :: Res_Pat :: Res_Pat_
  {{ com under constraints $[[N]]$ , $[[res]]$ will produce a normalised resourced context if matched against $[[out_res_pat]]$ }} by

    ---------------- :: Empty
    N |- emp ~> emp

    ----------------- :: Phi
    N |- term ~> TERM


    smt ( N => term )
    N |- res1 ~> res_pat
    ---------------------------------------------- :: If_True
    N |- if term then res1 else res2 ~> res_pat


    smt ( N => neg term )
    N |- res2 ~> res_pat
    ---------------------------------------------- :: If_False
    N |- if term then res1 else res2 ~> res_pat


    ------------- :: Var
    N |- norm_res ~> r


    N |- res1 ~> res_pat1
    N |- res2 ~> res_pat2
    ------------------------------------------------ :: SepConj
    N |- res1 * res2 ~> < res_pat1 , res_pat2 >


    N |- x / y ( res ) ~> res_pat
    ------------------------------------------------------ :: Pack
    N |- exists y : base_type .: res ~> pack ( x , res_pat )



  defn N |- ret ~> out_ret_pat :: :: Ret_Pat :: Ret_Pat_
  {{ com under constraints $[[N]]$ , $[[ret]]$ will produce a normalised resourced context if matched against $[[out_ret_pat]]$ }} by

  --------- :: I
  N |- I ~>


  N |- res ~> res_pat
  N |- ret ~> ret_pat
  ------------------------------------------------------------ :: Res
  N |- res *o ret ~> reso res_pat , ret_pat


  N |- ret ~> ret_pat
  ------------------------------------------------------------ :: Log
  N |- exists x : base_type .: ret ~> log x , ret_pat


  defn N ; nR ~> out_res_bind :: :: Simp_Ctx :: Simp_Ctx_
  {{ com contexts $[[N]]; [[nR]]$ can be partially simplified using $[[out_res_bind]]$ }} by

    ---------------- :: Empty
    N ; cdot ~> cdot

    N |- simp ( norm_res ) ~> res'
    N |- res' ~> res_pat
    N ; nR ~> res_bind
    ----------------------------------------------------------- :: Simp
    N ; nR , x : norm_res ~> res_bind , res_pat : res' = x


    N |- simp ( res ) ~> None
    N ; nR ~> res_bind
    --------------------------------- :: Skip
    N ; nR , _x : norm_res ~> res_bind


  % DEFER out_stmt
  defn CLNnR |- stmt <= norm_ret ~> stmt' :: :: Stmt :: Stmt_
  {{ com given contexts $[[CLNnR]]$, $[[stmt]] {:} [[norm_ret]]$ elaborates to $[[stmt']]$ }} by


    C ; L ; N ; nR |- ret_terms :: to_fun norm_ret ~> bind res_bind for ret_terms' and I -| nR'
    ----------------------------------------------------------------------------------------- :: RetT
    C ; L ; N ; nR |- < ret_terms > <= norm_ret ~> insert_lets ( res_bind , < ret_terms' > )


    ---------------------------------------------------------------- :: Pure
    C ; L ; N ; nR |- pure ( pstmt ) <= pure_ret ~> pure ( pstmt )


    C ; L ; N ; nR |- expr ~> bind res_bind for expr' : ret -| nR'
    N |- ret ~> ret_pat
    N |- ret_pat : sigma y : base_type .: ret ~> C1 ; L1 ; N1 ; nR1
    C , C1 ; L , L' , L1 ; N , N' , N1 ; nR' , nR1 |- stmt <= norm_ret2 ~> stmt'
    stmt'' def= insert_lets ( res_bind , let ret_pat = expr' in stmt' )
    ---------------------------------------------------------------------------------------- :: Let
    C ; L ; N ; nR |- let _ret_pat = expr in stmt <= norm_ret2 ~> stmt''


    C ; L ; N ; nR' |- stmt1 <= norm_ret1 ~> stmt1'
    N |- norm_ret1 ~> ret_pat
    N |- ret_pat : norm_ret1 ~> C1 ; L1 ; N1 ; nR1
    C , C1 ; L , L1 ; N , N1 ; nR , nR1 |- stmt2 <= norm_ret2 ~> stmt2'
    stmt'' def= let ret_pat : norm_ret1 = stmt1' in stmt2'
    ---------------------------------------------------------------------- :: Annot
    C ; L ; N ; nR' , nR |- let _ret_pat : norm_ret1 = stmt1 in stmt2 <= norm_ret2 ~> stmt''


    C |- pval => bool
    N , inj pval = true ; nR ~> res_bind1
    C ; L ; N , inj pval = true  ; nR1 |- insert_lets ( res_bind1 , stmt1 ) <= norm_ret ~> stmt1'
    N , inj pval = false ; nR2 ~> res_bind2
    C ; L ; N , inj pval = false ; nR2 |- insert_lets ( res_bind2 , stmt2 ) <= norm_ret ~> stmt2'
    ------------------------------------------------------------------------- :: If
    C ; L ; N ; nR |- if pval then stmt1 else stmt2 <= norm_ret ~> if pval then stmt1' else stmt2'


    C |- pval => base_type1
    indexed pati : base_type1 ~> Ci with termi
    indexed </ N , termi = inj pval ; nR ~> res_bindi // i />
    indexed </ C , Ci ; L ; N , termi = inj pval ; nR |- insert_lets ( res_bindi , stmti ) <= norm_ret ~> stmt'i // i />
    ------------------------------------------------------------------------------------------------------ :: Case
    C ; L ; N ; nR |- case pval of </ | pati => stmti // i /> end <= norm_ret ~> case pval of </ | pati => stmt'i // i /> end


    P ( id ) = </ xi // i /> |-> stmt : norm_fun
    C ; L ; N ; nR |- </ xi = spine_elemi // i /> :: norm_fun >> subs ; false /\ I
    ----------------------------------------------------------------------------- :: Run
    C ; L ; N ; nR |- run id </ spine_elemi // i /> <= false /\ I ~> run id </ spine_elemi // i />
